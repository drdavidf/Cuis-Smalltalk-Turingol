tape alphabet is _, a, b, c, x ;

to move right until $x or $y 
{

loop: 

	move right one square; 

	if tape symbol is $x then go to exit ; 

	if tape symbol is $y then go to exit ; 

	go to loop ;

exit: ;

}

to move left until $x
{

loop:

	move left one square; 

	if tape symbol is $x then go to exit ; 

	go to loop ;

exit: ;

}

to move left until $x or $y or $z
{

loop: 

	move left one square; 

	if tape symbol is $x then go to exit ; 

	if tape symbol is $y then go to exit ; 

	if tape symbol is $z then go to exit ; 

	go to loop ;

exit: ;

}

;

loop: 

move right until "a" or "_";

if tape symbol is "_" then go to finish ;

print "x"		; 

move right until "b" or "_";
	
if tape symbol is "_" then reject ;

print "x" ; 

move right until "c" or "_";

if tape symbol is "_" then reject ;

print "x" ; 

move left until "_" ; 

go to loop;

finish: 

move left until "b" or "c" or "_" ;

if tape symbol is "_" then accept ;

reject.

